plugins {
    id 'com.zeroc.gradle.ice-builder.slice' version '1.5.0' apply false
    id 'com.github.johnrengelman.shadow' version '8.1.1' apply false
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'com.zeroc.gradle.ice-builder.slice'

    repositories {
        mavenCentral()
    }

    dependencies {
        implementation 'com.zeroc:ice:3.7.10'
        implementation 'com.google.code.gson:gson:2.10.1'
        implementation 'org.postgresql:postgresql:42.6.0'
    }

    slice {
        java {
            files = [file("../VotacionXYZ.ice")]
        }
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
    }

    jar {
        manifest {
            attributes(
                "Main-Class": project.name.capitalize(),
                "Class-Path": configurations.runtimeClasspath.resolve().collect { it.toURI() }.join(' ')
            )
        }
    }
}

project(":central") {
    apply plugin: 'com.github.johnrengelman.shadow'

    shadowJar {
        archiveBaseName.set("central-all")
        archiveClassifier.set("")
        archiveVersion.set("")
        manifest {
            attributes(
                'Main-Class': 'Central'
            )
        }
    }

    build.dependsOn(shadowJar)
}
